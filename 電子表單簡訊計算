Option Explicit

Sub 第一步合併報表()
    Dim fileDialog As fileDialog
    Dim selectedFiles As Collection
    Dim mergedWorkbook1 As Workbook, mergedWorkbook2 As Workbook, currentWorkbook As Workbook
    Dim dataSheet As Worksheet, rngData As Range
    Dim totalRows1 As Long, totalRows2 As Long, lastRow As Long, outputPath As String
    Dim totalPointsNonElectronic As Double
    Dim filePath As Variant, smsContentLong As String, smsContentShort As String
    Dim name As String, points As Double, isElectronic As Boolean
    Dim i As Long
    Dim resultsSheet As Worksheet

    ' 讓使用者選擇要合併的檔案
    Set fileDialog = Application.fileDialog(msoFileDialogFilePicker)
    fileDialog.AllowMultiSelect = True
    fileDialog.Title = "選擇要合併的 Excel 檔案"
    fileDialog.Filters.Clear
    fileDialog.Filters.Add "Excel 檔案", "*.xls; *.xlsx"

    If fileDialog.Show = False Then
        MsgBox "未選擇檔案", vbInformation
        Exit Sub
    End If

    ' 儲存選擇的檔案路徑
    Set selectedFiles = New Collection
    For Each filePath In fileDialog.SelectedItems
        selectedFiles.Add filePath
    Next filePath

    ' 建立合併的工作簿
    Set mergedWorkbook1 = Application.Workbooks.Add
    mergedWorkbook1.Sheets(1).name = "MergedData"
    Set mergedWorkbook2 = Application.Workbooks.Add
    mergedWorkbook2.Sheets(1).name = "MergedData2"

    ' 初始化行數計數器
    totalRows1 = 1 ' 第一個工作簿的行數計數器
    totalRows2 = 1 ' 第二個工作簿的行數計數器
    
    For Each filePath In selectedFiles
        On Error Resume Next
        Set currentWorkbook = Workbooks.Open(filePath)
        If Err.Number <> 0 Then
            MsgBox "無法開啟檔案：" & filePath & vbCrLf & "錯誤信息：" & Err.Description, vbCritical
            On Error GoTo 0
            Exit Sub
        End If
        
        On Error GoTo 0
        
        ' 遍歷每個工作表
        For Each dataSheet In currentWorkbook.Sheets
            
            lastRow = dataSheet.Cells(dataSheet.Rows.Count, 1).End(xlUp).Row
            
            If lastRow > 1 Then ' 確保工作表有資料
                ' 複製有效範圍（假設從第 1 列到最後一列）
                Set rngData = dataSheet.Range("A1").CurrentRegion
                
                ' 檢查是否超過 100 萬筆限制
                If totalRows1 + rngData.Rows.Count - 1 <= 1000000 Then
                    ' 複製到第一個合併工作簿中
                    rngData.Copy Destination:=mergedWorkbook1.Sheets(1).Cells(totalRows1, 1)
                    totalRows1 = totalRows1 + rngData.Rows.Count ' 更新第一個工作簿的行數計數器
                    
                ElseIf totalRows2 + rngData.Rows.Count - 1 <= 1000000 Then
                    ' 複製到第二個合併工作簿中，並更新行數以防止溢出限制
                    rngData.Copy Destination:=mergedWorkbook2.Sheets(1).Cells(totalRows2, 1)
                    totalRows2 = totalRows2 + rngData.Rows.Count ' 更新第二個工作簿的行數計數器
                    
                Else
                    MsgBox "資料量超過限制，無法處理更多資料。", vbCritical
                    Exit Sub
                End If
                
            End If
        Next dataSheet
        
        currentWorkbook.Close SaveChanges:=False
    Next filePath

End Sub

Sub 第二步插入欄位並下移儲存格()
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim filePaths As Variant
    Dim i As Integer

    ' 顯示對話框讓用戶選擇多個工作簿
    filePaths = Application.GetOpenFilename("Excel Files (*.xls; *.xlsx), *.xls; *.xlsx", , "選擇工作簿", , True)

    ' 檢查用戶是否選擇了文件
    If IsArray(filePaths) Then
        For i = LBound(filePaths) To UBound(filePaths)
            ' 打開選擇的工作簿
            Set wb = Workbooks.Open(filePaths(i))
            
    Worksheets(1).Rows(1).Insert Shift:=xlShiftDown ' 插入並下移儲存格
 
            ' 假設在第一個工作表中插入行
            Set ws = wb.Sheets(1) ' 或者可以指定具體的工作表名稱
           

            ' 填入指定值
        ws.Cells(1, 1).Value = "發送序號" ' A欄
        ws.Cells(1, 2).Value = "三竹序號" ' B欄
        ws.Cells(1, 3).Value = "部門" ' C欄
        ws.Cells(1, 4).Value = "發送者" ' D欄
        ws.Cells(1, 5).Value = "預約時間" ' E欄
        ws.Cells(1, 6).Value = "狀態時間" ' F欄
        ws.Cells(1, 7).Value = "簡訊狀態" ' G欄
        ws.Cells(1, 8).Value = "批次名稱" ' H欄
        ws.Cells(1, 9).Value = "報表名稱" ' I欄
        ws.Cells(1, 10).Value = "行動電話" ' J欄
        ws.Cells(1, 11).Value = "簡訊類型" ' K欄
        ws.Cells(1, 12).Value = "點數" ' L欄
        ws.Cells(1, 13).Value = "字數" ' M欄
        ws.Cells(1, 14).Value = "簡訊內容" ' N欄

            ' 保存並關閉工作簿
            wb.Save
        Next i
    Else
        MsgBox "未選擇任何工作簿。"
    End If
End Sub

Sub 第三步統計電子表單發送點數()
    Dim wb As Workbook
    Dim wsData As Worksheet
    Dim wsPivot As Worksheet
    Dim ws As Worksheet
    Dim fd As fileDialog
    Dim selectedFiles() As String
    Dim rng As Range
    Dim lastRow As Long, pasteRow As Long
    Dim i As Integer
    Dim pivotTable As pivotTable
    Dim pivotCache As pivotCache
    Dim pivotRange As Range
    Dim fileIndex As Integer

    ' 建立新的活頁簿並命名
    Set wb = Workbooks.Add
    wb.SaveAs ThisWorkbook.Path & "\Text msg pivot.xlsx"

    ' 新增工作表存放數據
    On Error Resume Next
    Set wsData = wb.Sheets("Text msg pivot")
    If wsData Is Nothing Then
        Set wsData = wb.Sheets.Add
        wsData.name = "Text msg pivot"
    End If
    On Error GoTo 0

    ' 設定標題
    wsData.Cells(1, 1).Value = "名稱"
    wsData.Cells(1, 2).Value = "點數"
    wsData.Cells(1, 3).Value = "內容"

    ' 讓使用者選擇檔案
    Set fd = Application.fileDialog(msoFileDialogFilePicker)
    fd.AllowMultiSelect = True
    fd.Title = "選擇 Excel 檔案"
    fd.Filters.Clear
    fd.Filters.Add "Excel 檔案", "*.xlsx; *.xlsm; *.xls", 1

    Debug.Print "FileDialog 準備顯示"

    If fd.Show = -1 Then
        ' 初始化 selectedFiles 陣列
        ReDim selectedFiles(fd.SelectedItems.Count - 1)
        
        ' 將選擇的檔案存入陣列
        For i = 1 To fd.SelectedItems.Count
            selectedFiles(i - 1) = fd.SelectedItems(i)
        Next i
    Else
        Debug.Print "使用者取消選擇"
        Exit Sub
    End If

    pasteRow = 2 ' 初始貼上列

    ' 遍歷選擇的 Excel 檔案
    Application.ScreenUpdating = False
    Application.Calculation = xlCalculationManual

    For fileIndex = LBound(selectedFiles) To UBound(selectedFiles)
        Dim srcWb As Workbook
        Set srcWb = Workbooks.Open(selectedFiles(fileIndex), ReadOnly:=True)

        ' 假設資料來自第一個工作表
        Set ws = srcWb.Sheets(1)

        ' 找到最後一列
        lastRow = ws.Cells(ws.Rows.Count, "N").End(xlUp).Row

        ' 遍歷 N 欄，找到包含 "mlitw" 或 "mliutw" 的列
        Dim j As Long
        For j = 2 To lastRow
            If ws.Cells(j, "N").Value Like "*mlitw*" Or ws.Cells(j, "N").Value Like "*mliutw*" Then
                wsData.Cells(pasteRow, 1).Value = ws.Cells(j, "I").Value ' 名稱
                wsData.Cells(pasteRow, 2).Value = ws.Cells(j, "L").Value ' 點數
                wsData.Cells(pasteRow, 3).Value = ws.Cells(j, "N").Value ' 內容
                pasteRow = pasteRow + 1
            End If
        Next j

        srcWb.Close False ' 關閉來源檔案
    Next fileIndex

    Application.Calculation = xlCalculationAutomatic
    Application.ScreenUpdating = True

    ' 新增樞紐分析表的工作表
    On Error Resume Next
    Set wsPivot = wb.Sheets("統計分析")
    If wsPivot Is Nothing Then
        Set wsPivot = wb.Sheets.Add
        wsPivot.name = "統計分析"
    End If
    On Error GoTo 0

    ' 建立樞紐分析表
    Set pivotRange = wsData.Range("A1").CurrentRegion
    Set pivotCache = wb.PivotCaches.Create(SourceType:=xlDatabase, SourceData:=pivotRange)
    Set pivotTable = pivotCache.CreatePivotTable(TableDestination:=wsPivot.Cells(1, 1), TableName:="點數統計")

With pivotTable
    ' 設定「名稱」為列標籤，讓報表名稱分類顯示
    .PivotFields("名稱").Orientation = xlRowField
    .PivotFields("名稱").Position = 1

    ' 設定「點數」為數值欄位，計算總和
    Dim dataField As PivotField
    Set dataField = .AddDataField(.PivotFields("點數"), "總點數", xlSum)
    dataField.NumberFormat = "#,##0" ' 格式化數值

    ' 顯示「名稱」對應的各別點數
    Dim individualField As PivotField
    Set individualField = .PivotFields("名稱")
    individualField.Subtotals(1) = True ' 啟用分類小計
End With


    MsgBox "統計完成，請查看 'Text msg pivot.xlsx'!", vbInformation
End Sub

Sub 第四步統計總花費點數()

  Dim filePaths As Variant
  Dim i As Long
  Dim wb As Workbook, newWb As Workbook
  Dim ws As Worksheet, newWs As Worksheet
  Dim sum As Double

  ' 讓使用者選擇 Excel 檔案
  filePaths = Application.GetOpenFilename( _
    fileFilter:="Excel Files (*.xls*), *.xls*", _
    MultiSelect:=True)

  ' 建立新的活頁簿
  Set newWb = Workbooks.Add
  Set newWs = newWb.Sheets(1)

  ' 設定欄位標題
  newWs.Cells(1, 1).Value = "總花費點數"

  ' 逐個開啟選擇的 Excel 檔案
  For i = 1 To UBound(filePaths)

    ' 開啟 Excel 檔案
    Set wb = Workbooks.Open(filePaths(i))
    Set ws = wb.Sheets(1) ' 假設資料在第一個工作表

    ' 計算 L 欄的總和
    sum = Application.WorksheetFunction.sum(ws.Columns("L"))

    ' 將總和寫入新的活頁簿
    newWs.Cells(i + 1, 1).Value = sum

    ' 關閉已開啟的 Excel 檔案
    wb.Close SaveChanges:=False

  Next i

End Sub




